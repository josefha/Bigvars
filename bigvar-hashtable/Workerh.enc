
module Workerh

import HashEntry
import Siphash
import Hasher

import Bighash

active class Worker[k,v]
    var id : int
    var tableSize : int
    var numberOfElements: int
    var table : [HashEntry[k,v]]
    var siphash : Siphash
    var hashFunction : k -> uint

    def init(size: int, id:int, f:k -> uint) : unit
        this.hashFunction = f
        this.numberOfElements = 0
        this.tableSize = size
        this.id = id
        this.siphash = new Siphash()
        this.initTable()
    end

    def modulo(value : uint, mod : uint) : uint
      EMBED (uint)
        uint64_t value = (uint64_t) #{value};
              uint64_t mod   = (uint64_t) #{mod};
              (value % mod);

      END
    end

    def initTable() : unit
        this.table = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            this.table(i) = new HashEntry[k,v]()
        end
    end

    def getInfo() : unit
        println("w: {} has: {}/{}", this.id, this.numberOfElements, this.tableSize)
        --this.numberOfElements
    end

    def put(key:k,value:v,hashValue:uint) : unit
        if (this.tableSize - this.numberOfElements < this.tableSize/4) then
            this.rehash()
        end

        var hashentry = this.getEntry(key,hashValue)

        if (hashentry.getKey() != key) then
            this.numberOfElements += 1
        end
        hashentry.add(key,value,hashValue)
    end

    def get(key:k,hashValue:uint) : v
        var hashentry = this.getEntry(key,hashValue)
        hashentry.getValue()
    end

    def remove(key:k,hashValue:uint) : unit
        var entryID = this.modulo(hashValue,this.tableSize)
        var hashentry = this.table(entryID)
        if (hashentry.notEmpty()) then
            if (hashentry.getKey() == key) then
                this.numberOfElements -= 1
                this.table(entryID) = new HashEntry[k,v]()
            end
        end
    end

    def elements() : [v]
        var result = new [v](this.numberOfElements)
        var index = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                result(index) = this.table(i).getValue()
                index += 1
            end
        end
        result
    end

    def keys() : [k]
        var count = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                count += 1
            end
        end

        var result = new [k](count)
        var index = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                result(index) = this.table(i).getKey()
                index += 1
            end
        end
        result
    end

    def doubleSize() : unit
        this.numberOfElements = 0
        this.tableSize = this.tableSize*2
        var newTable = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            newTable(i) = new HashEntry[k,v]()
        end
        this.table = newTable
    end

    def clear() : unit
        this.numberOfElements = 0
        var newTable = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            newTable(i) = new HashEntry[k,v]()
        end
        this.table = newTable
    end

    def rehash() : unit
        var elements = this.elements()
        var keys = this.keys()
        this.doubleSize()

        repeat i <- |elements| do
            var hash = this.generateHash(keys(i))
            this.put(keys(i),elements(i),hash)
        end
    end

    def hasKey(key:k, hashValue:uint) : bool
        var result = false
        var hashentry = this.getEntry(key,hashValue)
        if hashentry.notEmpty() then
            result = true
        end
        result
    end

    def extend(key:k,value:v,hashValue:uint) : unit
        var hashentry = this.getEntry(key,hashValue)
        if (hashentry.getKey() != key) then
            this.numberOfElements += 1
        end

        hashentry.extend(key,value,hashValue)
    end

    def extendAll(key:k, values:[v],hashValue:uint) : unit
        var hashentry = this.getEntry(key,hashValue)
        hashentry.extendAll(key,values,hashValue)
    end

    def getValues(key:k,hashValue:uint) : [v]
        var hashentry = this.getEntry(key,hashValue)
        hashentry.getValues()
    end

    def getEntry(key:k,hashValue:uint) : HashEntry[k,v]
        var entryID = this.modulo(hashValue,this.tableSize)
        var hashentry = this.table(entryID)
        while ((hashentry.notEmpty()) && (hashentry.getHash() != hashValue)) do --(hashentry.getKey() != key)) do
            entryID = this.modulo(entryID+1,this.tableSize)
            hashentry = this.table(entryID)
        end
        hashentry
    end

    -- types here...
    def mapper(m:(k,v)->[(v,k)], bighash:Bighash[v,k]) : unit
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                for result <- this.table(i).apply(m) do
                    var key = result.0
                    var value = result.1
                    bighash.extend(key,value)
                end
            end
        end
    end

    def reducer(r:(k,[v]) -> (k,v),bighash:Bighash[k,v]) : unit
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                var result = this.table(i).reduce(r)
                var key = result.0
                var value = result.1
                bighash.put(key,value)
            end
        end
    end

    def generateHash(key:k) : uint
        var k = key
        this.siphash.hash(this.hashFunction(k))
    end
end
