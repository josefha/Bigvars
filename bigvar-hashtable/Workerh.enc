
module Workerh

import HashEntry
import Combiner

import Bighash -- for mapper and reducer


active class Worker[k,v]
    var id : int
    var tableSize : int
    var filledEntries: int
    var table : [HashEntry[k,v]]
    var hashFunction : k -> uint

    def init(size: int, id:int, f:k -> uint) : unit
        this.hashFunction = f
        this.filledEntries = 0
        this.tableSize = size
        this.id = id
        this.initTable()
    end

    def initTable() : unit
        this.table = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            this.table(i) = new HashEntry[k,v]()
        end
    end

    def get(key:k,hashValue:uint) : v
        this.getEntry(hashValue).getValue()
    end

    def remove(key:k,hashValue:uint) : unit
        var hashentry = this.getEntry(hashValue)

        repeat i <- this.tableSize do
            if this.table(i).getHash() == hashValue then
                this.table(i) = new HashEntry[k,v]()
                this.filledEntries -= 1
            end
        end
    end

    def put(key:k,value:v,hashValue:uint) : unit
        this.resizeIfNeeded()
        var hashentry = this.getEntry(hashValue)
        if (hashentry.hasEntry == false) then this.filledEntries += 1 end
        hashentry.add(key,value,hashValue)
    end

    def hasKey(key:k, hashValue:uint) : bool
        var result = false
        var hashentry = this.getEntry(hashValue)
        if hashentry.notEmpty() then
            result = true
        end
        result
    end

    def elements() : [v]
        var result = new [v](this.filledEntries)
        var index = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                result(index) = this.table(i).getValue()
                index += 1
            end
        end
        result
    end

    def keys() : [k]
        var count = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                count += 1
            end
        end

        var result = new [k](count)
        var index = 0
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                result(index) = this.table(i).getKey()
                index += 1
            end
        end
        result
    end

    def clear() : unit
        this.filledEntries = 0
        var newTable = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            newTable(i) = new HashEntry[k,v]()
        end
        this.table = newTable
    end

    def extend(key:k,value:v,hashValue:uint) : unit
        this.resizeIfNeeded()
        var hashentry = this.getEntry(hashValue)
        if (hashentry.hasEntry == false) then this.filledEntries += 1 end
        hashentry.extend(key,value,hashValue)
    end


    def extendAll(key:k, values:[v],hashValue:uint) : unit
        var hashentry = this.getEntry(hashValue)
        if (hashentry.hasEntry == false) then this.filledEntries += 1 end
        hashentry.extendAll(key,values,hashValue)
    end

    def getValues(key:k,hashValue:uint) : [v]
        var hashentry = this.getEntry(hashValue)
        hashentry.getValues()
    end

    def getEntry(hashValue:uint) : HashEntry[k,v]
        var entryID = this.modulo(hashValue,this.tableSize)
        var hashentry = this.table(entryID)
        while ((hashentry.notEmpty()) && (hashentry.getHash() != hashValue)) do
            entryID = this.modulo(entryID+1,this.tableSize)
            hashentry = this.table(entryID)
        end
        hashentry
    end

    def resizeIfNeeded() : unit
        if (this.tableSize - this.filledEntries < this.tableSize/4) then
            this.rehash()
        end
    end

    def rehash() : unit
        println("Rehashing {}",this.id)
        var oldTable = this.table
        this.table = this.newTable()

        repeat i <- |oldTable| do
            if this.table(i).notEmpty() then
                var oldEntry = this.table(i)
                this.replace(oldEntry)
            end
        end
    end

    def replace(oldEntry : HashEntry[k,v]) : unit
        var hashValue = oldEntry.getHash()
        var entryID = this.modulo(hashValue,this.tableSize)
        var hashentry = this.table(entryID)
        while ((hashentry.notEmpty()) && (hashentry.getHash() != hashValue)) do
            entryID = this.modulo(entryID+1,this.tableSize)
            hashentry = this.table(entryID)
        end

        this.table(entryID) = oldEntry
    end

    def newTable() : [HashEntry[k,v]]
        this.tableSize = this.tableSize*2
        var newTable = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            newTable(i) = new HashEntry[k,v]()
        end
        newTable
    end

    def doubleSize() : unit
        this.filledEntries = 0
        this.tableSize = this.tableSize*2
        var newTable = new [HashEntry[k,v]](this.tableSize)
        repeat i <- this.tableSize do
            newTable(i) = new HashEntry[k,v]()
        end
        this.table = newTable
    end

    def modulo(value : uint, mod : uint) : uint
      EMBED (uint)
        uint64_t value = (uint64_t) #{value};
              uint64_t mod   = (uint64_t) #{mod};
              (value % mod);

      END
    end

    def getInfo() : unit
        println("w: {} has: {}/{}", this.id, this.filledEntries, this.tableSize)
    end

    ----- MapReduce functions ------
    -- def mapper[k2,v2](m:(k,v)->[(k2,v2)],r:(k2,[v2]) -> (k2,v2),bighash:Bighash[k2,v2]) : unit
    --     var combiner = new Combiner[k2,v2](5000) --fix this constant or make combiner resizeible
    --     var result = bighash.copy()
    --
    --     repeat i <- this.tableSize do
    --         if this.table(i).notEmpty() then
    --             var mapresult = this.table(i).map[k2,v2](m)
    --
    --             for pair <- mapresult do
    --                 var hashfunction = bighash.hashFunction()
    --                 var hash = hashfunction(pair.0)
    --                 combiner.extend(pair.0,pair.1,hash)
    --             end
    --         end
    --     end
    --     combiner.combine(r,consume result)
    -- end

    def mapper[k2,v2](m:(k,v)->[(k2,v2)],r:(k2,[v2]) -> (k2,v2),bighash:Bighash[k2,v2]) : unit
        var combiner = new Combiner[k2,v2](5000) --fix this constant or make combiner resizeible
        var result = bighash.copy()

        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                var mapresult = this.table(i).map[k2,v2](m)

                for pair <- mapresult do
                    var hashfunction = bighash.hashFunction()
                    var hash = hashfunction(pair.0)
                    combiner.extend(pair.0,pair.1,hash)
                end
            end
        end
        combiner.combine(r,consume result)
    end

    def reducer(r:(k,[v]) -> (k,v),bighash:Bighash[k,v]) : unit
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                var result = this.table(i).reduce(r)
                var key = result.0
                var value = result.1
                bighash.put(key,value)
            end
        end
    end

    def mapperNoCombiner[k2,v2](m:(k,v)->[(k2,v2)], bighash:Bighash[k2,v2]) : unit
        repeat i <- this.tableSize do
            if this.table(i).notEmpty() then
                for result <- this.table(i).map[k2,v2](m) do
                    var key = result.0
                    var value = result.1
                    bighash.extend(key,value)
                end
            end
        end
    end
end
